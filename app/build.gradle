apply plugin: 'android'

android {
    def versionMajor = 0
    def versionMinor = 4
    def versionPatch = 1
    def versionBuild = 0

    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    def Properties localProps = new Properties()
    localProps.load(new FileInputStream(file('../local.properties')))
    def Properties keyProps = new Properties()

    signingConfigs {
        mars3142 {
            if (localProps['keystore.props.file']) {
                keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
                storeFile file(keyProps["store"])
                keyAlias keyProps["alias"]
                storePassword keyProps["storePass"]
                keyPassword keyProps["pass"]
            }
        }
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    buildTypes {
        debug {
            if (localProps['keystore.props.file']) {
                signingConfig signingConfigs.mars3142
            }
        }
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            if (localProps['keystore.props.file']) {
                signingConfig signingConfigs.mars3142
            }
        }
    }
}

dependencies {
    // from google
    // compile 'com.android.support:support-v4:+'
    compile 'com.android.support:appcompat-v7:+'
    // compile 'com.google.android.gms:play-services:+'

    // card library
    compile 'com.github.gabrielemariotti.cards:library:+'
    compile 'com.github.gabrielemariotti.cards:library-extra:+'
}
