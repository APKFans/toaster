apply plugin: 'com.android.application'

android {
    def versionMajor = 0
    def versionMinor = 4
    def versionPatch = 1
    def versionBuild = 0

    buildToolsVersion '20'
    compileSdkVersion 20

    def Properties localProps = new Properties()
    localProps.load(new FileInputStream(file('../local.properties')))
    def Properties keyProps = new Properties()

    signingConfigs {
        mars3142 {
            if (localProps['keystore.properties']) {
                keyProps.load(new FileInputStream(file(localProps['keystore.properties'])))
                storeFile file(keyProps["store"])
                keyAlias keyProps["alias"]
                storePassword keyProps["storePass"]
                keyPassword keyProps["pass"]
            }
        }
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 20
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix ".debug"
            if (localProps['keystore.properties']) {
                signingConfig signingConfigs.mars3142
            }
        }
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            if (localProps['keystore.properties']) {
                signingConfig signingConfigs.mars3142
            }
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:20.0.0'

    /* card library */
    compile 'com.github.gabrielemariotti.cards:library:1.9.1'
    compile 'com.github.gabrielemariotti.cards:library-extra:1.9.1'
}