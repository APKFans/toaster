buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url "https://github.com/novoda/public-mvn-repo/raw/master/releases"
    }
}

android {
    def versionMajor = 0
    def versionMinor = 4
    def versionPatch = 0
    def versionBuild = 2

    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    signingConfigs {
        release {
            storeFile file(System.getenv("KEY_STORE"))
            storePassword System.getenv("STORE_PW")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PW")
        }
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                def file = variant.outputFile
                variant.outputFile = new File(file.parent, file.name.replace(".apk", "-" + defaultConfig.versionCode + ".apk"))
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:19.0.1'
    compile 'com.android.support:appcompat-v7:19.0.1'
    compile 'com.github.gabrielemariotti.cards:library:1+'
    compile 'com.novoda:sqliteprovider-core:1+'
}
